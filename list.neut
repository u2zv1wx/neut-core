;; preliminary.

(no-implicit-core)

;; for test
;; (ensure libarchive/3.4.2
;;   "https.//www.libarchive.org/downloads/libarchive-3.4.2.tar.gz")

(include "core/notation.neut")

(include "core/option.neut")

(include "core/bool.neut")

(include "core/identity.neut")

(include "core/string.neut")

(include "core/integer.neut")

(use option)

(use bool)

(use lazy)

;; content.

(section list)

(inductive list ((a tau))
  (nil
    ()
    (list a))
  (cons
    ((_ a)
     (_ (list a)))
    (list a)))

(notation (list.new)
  (list.nil))

(notation (list.new a)
  (list.cons * a (list.nil *)))

(notation (list.new a rest+)
  (list.cons * a (list.new rest+)))

(define head
  ((a tau)
   (xs (list a)))
  (witness (option a)
    (list.case * * xs
      (λ ()
        (none a))
      (λ (h _)
        (some a h)))))

(define length
  ((a tau)
   (xs (list a)))
  (witness i64
    (list.case i64 a xs
      (λ ()
        0)
      (λ (_ ys)
        (add-i64 1 (length a ys))))))

(define append
  ((a tau)
   (xs (list a))
   (ys (list a)))
  (witness (list a)
    (list.case (list a) a xs
      (λ ()
        ys)
      (λ (z zs)
        (cons a z (append a zs ys))))))

(define is-null
  ((a tau)
   (xs (list a)))
  (witness bool
    (list.case bool a xs
      (λ ()
        true)
      (λ (_ _)
        false))))

(define map
  ((a tau)
   (b tau)
   (f (hom a b))
   (xs (list a)))
  (witness (list b)
    (list.case (list b) a xs
      (λ ()
        (nil b))
      (λ (y acc)
        (cons b (f y) (map a b f acc))))))

(define return
  ((a tau)
   (x a))
  (witness (list a)
    (list.new x)))

(define concatenate
  ((a tau)
   (m (list (list a))))
  (witness (list a)
    (list.case (list a) (list a) m
      (λ ()
        (nil *))
      (λ (xs xss)
        (append * xs (concatenate a xss))))))

(define bind
  ((a tau)
   (b tau)
   (m (list a))
   (k (hom a (list b))))
  (witness (list b)
    (concatenate * (map * * k m))))

(end list)
